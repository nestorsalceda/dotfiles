#  vim: set ft=zsh ts=4 sw=4 et:

pmodload 'helper'


function prompt_sorin_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_sorin_pwd="$MATCH"
    unset MATCH
  else
    _prompt_sorin_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_nestorsalceda_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_sorin_pwd
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Ruby info
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  # Get Virtual env info
  if (( $+functions[python-info] )); then
    python-info
  fi

  # Get Node env info
  if (( $+functions[node-info] )); then
    node-info
  fi
}

function prompt_nestorsalceda_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_nestorsalceda_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format ''
  zstyle ':prezto:module:editor:info:keymap:primary' format '%F{red}❯%F{green}❯%F{yellow}❯%f'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %F{yellow}❮%F{green}❮%F{red}❮%f'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %F{green}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format ':%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %F{blue}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %F{magenta}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %F{cyan}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %F{white}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '(%F{blue}git%f$(coalesce "%b" "%p" "%c")%s)' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '%F{red}rb:%v%f'

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '%F{220}py:%v%f'

  # Set node-info parameters
  zstyle ':prezto:module:node:info:version' format '%F{106}node:%v%f'

  # Define prompts.
  PROMPT='
%F{blue}%n@%m%f%F{white}:%~%f ${ruby_info[version]} ${node_info[version]} ${python_info[virtualenv]}
%(!. %B%F{red}#%f%b.)${editor_info[keymap]} '
  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}${git_info[rprompt]} ${git_info:+${(e)git_info[prompt]}}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_nestorsalceda_setup "$@"

# TODO:
# * Last executed status code
# * Background Jobs
